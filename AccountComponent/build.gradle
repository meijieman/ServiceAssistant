def accountComponentIsApplication = !accountComponentIsLibrary.toBoolean()

if (accountComponentIsApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.3"

    // 设置资源前缀
    resourcePrefix "account_"

    defaultConfig {
        if (accountComponentIsApplication) {
            applicationId "cn.xiaoxige.accountcomponent"
        }
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            if (accountComponentIsApplication) {
                manifest.srcFile 'src/main/AsApplicationAndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {

    implementation 'cn.xiaoxige.serviceassistant:core:1.0.2'
//    implementation project(':ServiceAssistantCore')
    kapt 'cn.xiaoxige.serviceassistant:processor:1.0.3'
    //    kapt project(':ServiceAssistantProcessor')
    implementation project(':AccountApi')
    implementation project(':LoginApi')
    implementation project(':AppApi')


    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}